pluginManagement {
    gradle.ext.agpVersion = '8.1.0'
    gradle.ext.googleServicesVersion = '4.4.0'
    gradle.ext.daggerVersion = '2.50'
    gradle.ext.detektVersion = '1.23.5'
    gradle.ext.kotlinVersion = '1.9.22'
    gradle.ext.kspVersion = '1.9.22-1.0.17'
    gradle.ext.measureBuildsVersion = '2.1.2'
    gradle.ext.navigationVersion = '2.7.7'
    gradle.ext.sentryVersion = '4.3.1'
    gradle.ext.violationCommentsVersion = '1.69.0'
    gradle.ext.protobufVersion = '0.9.4'
    gradle.ext.dependencyAnalysisVersion = '1.28.0'

    repositories {
        google()
        exclusiveContent {
            forRepository {
                maven {
                    url = uri("https://a8c-libs.s3.amazonaws.com/android")
                }
            }
            filter {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.measure-builds"
            }
        }
        gradlePluginPortal()
    }

    plugins {
        id 'androidx.navigation.safeargs.kotlin' version gradle.ext.navigationVersion
        id 'com.android.application' version gradle.ext.agpVersion
        id 'com.android.library' version gradle.ext.agpVersion
        id 'com.automattic.android.measure-builds' version gradle.ext.measureBuildsVersion
        id 'com.google.gms.google-services' version gradle.ext.googleServicesVersion
        id 'io.gitlab.arturbosch.detekt' version gradle.ext.detektVersion
        id 'io.sentry.android.gradle' version gradle.ext.sentryVersion
        id 'org.jetbrains.kotlin.android' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.jvm' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.plugin.parcelize' version gradle.ext.kotlinVersion
        id 'se.bjurr.violations.violation-comments-to-github-gradle-plugin' version gradle.ext.violationCommentsVersion
        id 'com.google.dagger.hilt.android' version gradle.ext.daggerVersion
        id 'com.google.devtools.ksp' version gradle.ext.kspVersion
        id "com.google.protobuf" version gradle.ext.protobufVersion
        id "com.autonomousapps.dependency-analysis" version gradle.ext.dependencyAnalysisVersion
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.17.2'
}

rootProject.name = 'WCAndroid'

include ':quicklogin'
include ':libs:commons'
include ':libs:cardreader'
include ':libs:iap'
include ':WooCommerce'

gradle.ext.fluxCBinaryPath = "org.wordpress:fluxc"
gradle.ext.fluxCWooCommercePluginBinaryPath = "org.wordpress.fluxc.plugins:woocommerce"
gradle.ext.loginFlowBinaryPath = "org.wordpress:login"
gradle.ext.mediaPickerBinaryPath = "org.wordpress:mediapicker"
gradle.ext.mediaPickerDomainBinaryPath = "org.wordpress.mediapicker:domain"
gradle.ext.mediaPickerSourceCameraBinaryPath = "org.wordpress.mediapicker:source-camera"
gradle.ext.mediaPickerSourceDeviceBinaryPath = "org.wordpress.mediapicker:source-device"
gradle.ext.mediaPickerSourceGifBinaryPath = "org.wordpress.mediapicker:source-gif"
gradle.ext.mediaPickerSourceWordPressBinaryPath = "org.wordpress.mediapicker:source-wordpress"

gradle.ext {
    compileSdkVersion = 34
    targetSdkVersion = 33
    minSdkVersion = 26
}

apply from: './config/gradle/include_builds.gradle'
apply from: './config/gradle/gradle_build_cache.gradle'
include ':WooCommerce-Wear'
