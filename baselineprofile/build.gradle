import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.baselineprofile'
}

repositories {
    maven {
        url 'https://a8c-libs.s3.amazonaws.com/android'
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.fluxc.plugins"
            includeGroup "org.wordpress.wellsql"
            includeGroup "org.wordpress.mediapicker"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
            includeGroup "com.gravatar"
            includeGroup "org.wordpress.aztec"
        }
    }
    maven {
        url 'https://zendesk.jfrog.io/zendesk/repo'
        content {
            includeGroup "com.zendesk"
            includeGroup "com.zendesk.belvedere2"
        }
    }
    maven {
        url "https://jitpack.io"
        content {
            includeModule("com.github.chrisbanes", "PhotoView")
            includeModule("com.github.PhilJay", "MPAndroidChart")
        }
    }
}

android {
    namespace 'com.woocommerce.android.baselineprofile'
    compileSdk gradle.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdk gradle.ext.minSdkVersion
        targetSdk gradle.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    targetProjectPath = ":WooCommerce"

    flavorDimensions += ["buildType"]
    productFlavors {
        vanilla { dimension = "buildType" }
    }

    // This code creates the gradle managed device used to generate baseline profiles.
    // To use GMD please invoke generation through the command line:
    // ./gradlew :WooCommerce:generateBaselineProfile
    testOptions.managedDevices.devices {
        pixel6Api34(ManagedVirtualDevice) {
            device = "Pixel 6"
            apiLevel = 34
            systemImageSource = "google"
        }
    }
}

// This is the configuration block for the Baseline Profile plugin.
// You can specify to run the generators on a managed devices or connected devices.
baselineProfile {
    managedDevices += "pixel6Api34"
    useConnectedDevices = false
}

dependencies {
    implementation "androidx.test.ext:junit:$jUnitExtVersion"
    implementation "androidx.test.espresso:espresso-core:$espressoVersion"
    implementation "androidx.test.uiautomator:uiautomator:$uiautomatorVersion"
    implementation "androidx.benchmark:benchmark-macro-junit4:$benchmarkVersion"
}

androidComponents {
    onVariants(selector().all()) { v ->
        def artifactsLoader = v.artifacts.getBuiltArtifactsLoader()
        v.instrumentationRunnerArguments.put(
                "targetAppId",
                v.testedApks.map { artifactsLoader.load(it)?.applicationId }
        )
    }
}
