plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'com.google.devtools.ksp'
}

repositories {
    maven {
        url 'https://a8c-libs.s3.amazonaws.com/android'
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.fluxc.plugins"
            includeGroup "org.wordpress.wellsql"
            includeGroup "org.wordpress.mediapicker"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
            includeGroup "com.gravatar"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
        content {
            includeVersion "com.android.volley", "volley", "1.1.1"
            includeVersion "com.google.android", "flexbox", "2.0.1"
            includeVersion "com.jraska", "falcon", "2.1.1"
            includeVersion "org.wordpress", "wellsql", "1.6.0"
            includeVersion "org.wordpress", "wellsql-core", "1.6.0"
        }
    }
}

android {
    namespace 'com.woocommerce.android.core'
    compileSdk 34

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig = true
        resValues = false
    }
}

dependencies {
    implementation "com.automattic:Automattic-Tracks-Android:$automatticTracksVersion"

    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.datastore:datastore:1.0.0"

    implementation platform('com.google.firebase:firebase-bom:32.7.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-analytics'

    implementation("org.wordpress:utils:$wordPressUtilsVersion") {
        exclude group: "com.mcxiaoke.volley"
        exclude group: "com.android.support"
    }

    implementation "org.apache.commons:commons-text:$commonsText"
    implementation "commons-io:commons-io:$commonsIO"

    implementation "com.google.dagger:hilt-android:$gradle.ext.daggerVersion"
    ksp "com.google.dagger:hilt-compiler:$gradle.ext.daggerVersion"
    ksp "androidx.hilt:hilt-compiler:$hiltJetpackVersion"

    api("${gradle.ext.fluxCBinaryPath}:$fluxCVersion") {
        exclude group: "com.android.support"
        exclude group: "org.wordpress", module: "utils"
    }
    api("${gradle.ext.fluxCWooCommercePluginBinaryPath}:$fluxCVersion") {
        exclude group: "com.android.support"
        exclude group: "org.wordpress", module: "utils"
    }
    api("com.automattic.tracks:experimentation:$automatticTracksVersion") {
        exclude group: "org.wordpress", module: "fluxc"
    }
    api project(":libs:cardreader")
}
