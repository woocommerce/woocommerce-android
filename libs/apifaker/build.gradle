plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

repositories {
    maven {
        url 'https://a8c-libs.s3.amazonaws.com/android'
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.fluxc"
            includeGroup "org.wordpress.fluxc.plugins"
            includeGroup "org.wordpress.wellsql"
            includeGroup "org.wordpress.mediapicker"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
        }
    }

    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
        content {
            includeVersion "com.android.volley", "volley", "1.1.1"
        }
    }
}

android {
    namespace 'com.woocommerce.android.apifaker'
    compileSdkVersion gradle.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion gradle.ext.minSdkVersion
        targetSdkVersion gradle.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"

    // Jetpack Compose
    implementation platform("androidx.compose:compose-bom:$composeBOMVersion")
    implementation 'androidx.activity:activity-compose'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.animation:animation'
    implementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.runtime:runtime-livedata'

    // Android Navigation for Compose
    implementation "androidx.navigation:navigation-compose:$gradle.ext.navigationVersion"

    implementation("${gradle.ext.fluxCBinaryPath}:$fluxCVersion") {
        exclude group: "com.android.support"
        exclude group: "org.wordpress", module: "utils"
    }

    implementation "com.google.dagger:hilt-android:$gradle.ext.daggerVersion"
    implementation "androidx.hilt:hilt-navigation-fragment:$hiltJetpackVersion"
    implementation "androidx.hilt:hilt-navigation-compose:$hiltJetpackVersion"
    kapt "com.google.dagger:hilt-compiler:$gradle.ext.daggerVersion"

    def room_version = "2.4.2"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
