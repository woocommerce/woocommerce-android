import com.automattic.android.measure.MeasureBuildsExtension
import io.gitlab.arturbosch.detekt.Detekt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'io.gitlab.arturbosch.detekt'
    id 'com.automattic.android.measure-builds'
    id "com.autonomousapps.dependency-analysis"
    id 'com.android.application' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id 'com.google.dagger.hilt.android' apply false
    id 'com.google.gms.google-services' apply false
    id 'com.google.devtools.ksp' apply false
}

measureBuilds {
    enable = findProperty('tracksEnabled')?.toBoolean() ?: false
    automatticProject = MeasureBuildsExtension.AutomatticProject.WooCommerce
    attachGradleScanId = true
    authToken = findProperty('appsMetricsToken')
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            allWarningsAsErrors = true
            freeCompilerArgs += [
                    "-opt-in=kotlin.RequiresOptIn",
                    "-Xjvm-default=all-compatibility",
            ]
        }
    }
}

subprojects {
    configurations {
        compile.exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
}

def detektAll = tasks.register("detektAll", Detekt) {
    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    buildUponDefaultConfig = true
    setSource(file(projectDir))
    baseline.set(file("$rootDir/config/detekt/baseline.xml"))
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    include("**/*.kt")
    exclude("**/resources/**", "**/build/**", "/vendor/**")

    reports {
        xml.required.set(true)
        html.required.set(true)
        txt.required.set(false)
    }
}

def detektAutoCorrectPrompt = tasks.register("detektAutoCorrectPrompt") {
    doLast {
        def detektTask = detektAll.get()
        if (detektTask.state.failure != null && !detektTask.autoCorrect) {
            println ".-----------------------------------------------------------------------------------------------------------------------------------.\n" +
                    "| ðŸ›   Did you know: you can ask Detekt to automatically resolve some of the issues by running `./gradlew detektAll --auto-correct` |\n" +
                    "'-----------------------------------------------------------------------------------------------------------------------------------'"
        }
    }
}

detektAll.configure{
    finalizedBy detektAutoCorrectPrompt
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Copies git-hooks from the `tools/team-props/git-hooks' directory to the `.git/hooks` folder
 * at the root of this project.
 */
tasks.register("installGitHooks", Copy) {
    println "Copying git-hooks scripts from tools/team-props/git-hooks to .git/hooks"
    from new File(rootProject.rootDir, 'tools/team-props/git-hooks')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

ext {
    fluxCVersion = 'trunk-e7f84fab139d1211941b37e1fc710207cb25eb47'
    glideVersion = '4.16.0'
    coilVersion = '2.1.0'
    constraintLayoutVersion = '1.2.0'
    libaddressinputVersion = '0.0.2'
    eventBusVersion = '3.3.1'
    googlePlayCoreVersion = '1.10.3'
    googlePlayWearableVersion = '18.1.0'
    coroutinesVersion = '1.7.3'
    lifecycleVersion = '2.6.2'
    aztecVersion = 'v1.3.45'
    flipperVersion = '0.176.1'
    stateMachineVersion = '0.2.0'
    coreKtxVersion = '1.12.0'
    appCompatVersion = '1.4.2'
    materialVersion = '1.6.1'
    hiltJetpackVersion = '1.1.0'
    wordPressUtilsVersion = '3.5.0'
    mediapickerVersion = '0.3.0'
    wordPressLoginVersion = '1.15.0'
    aboutAutomatticVersion = '0.0.6'
    automatticTracksVersion = '5.0.0'
    workManagerVersion = '2.7.1'
    billingVersion = '5.0.0'
    stripeTerminalVersion = '3.1.1'
    mlkitBarcodeScanningVersion = '17.2.0'
    mlkitTextRecognitionVersion = '16.0.0'
    androidxCameraVersion = '1.2.3'
    guavaVersion = '33.1.0-android'
    protobufVersion = '3.25.3'
    gravatarVersion = '0.2.0'

    // Apache
    commonsText = '1.10.0'
    commonsIO = '2.11.0'
    httpClientAndroidVersion = '4.3.5.1'

    // Compose and its module versions need to be consistent with each other (for example 'compose-theme-adapter')
    composeBOMVersion = "2024.04.00"
    composeCompilerVersion = "1.5.9"
    composeAccompanistVersion = "0.32.0"

    // Testing
    jUnitVersion = '4.13.2'
    jUnitExtVersion = '1.1.5'
    androidxTestCoreVersion = '1.4.0'
    assertjVersion = '3.24.1'
    espressoVersion = '3.4.0'
    mockitoKotlinVersion = '4.0.0'
    mockitoVersion = '4.6.1'
}

// Onboarding and dev env setup tasks
task checkBundler(type: Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task checkBundle(type: Exec, dependsOn: checkBundler) {
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task applyCredentials(type: Exec, dependsOn: checkBundle) {
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("configureApply") {
    group = 'Onboarding'
    description = 'Install dependencies for debug and production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}

apply from: './config/gradle/jacoco.gradle'
apply from: './config/gradle/gradle_build_scan.gradle'
