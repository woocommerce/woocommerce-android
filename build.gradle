buildscript {
    ext.kotlinVersion = '1.3.72'
    ext.navigationVersion = '2.3.0'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.automattic.android:fetchstyle:1.1'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

apply plugin: 'com.automattic.android.fetchstyle'

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        google()
        jcenter()
        maven {
            url 'http://www.idescout.com/maven/repo/'
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations {
        ktlint
        compile.exclude group: 'org.jetbrains' , module:'annotations-java5'
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.29.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }

    task ciktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Copies git-hooks from the `tools/team-props/git-hooks' directory to the `.git/hooks` folder
 * at the root of this project.
 */
task installGitHooks(type: Copy) {
    println "Copying git-hooks scripts from tools/team-props/git-hooks to .git/hooks"
    from new File(rootProject.rootDir, 'tools/team-props/git-hooks')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

ext {
    fluxCVersion = '1.6.25'
    daggerVersion = '2.29.1'
    glideVersion = '4.10.0'
    testRunnerVersion = '1.0.1'
    espressoVersion = '3.0.1'
    mockitoKotlinVersion = '2.1.0'
    mockitoVersion = '2.28.2'
    constraintLayoutVersion = '1.2.0'
    multidexVersion = '1.0.3'
    libaddressinputVersion = '0.0.2'
    eventBusVersion = '3.1.1'
    googlePlayCoreVersion = '1.8.0'
    coroutinesVersion = '1.3.6'
    archComponentsVersion = '2.2.0'
    assertjVersion = '3.11.1'
    aztecVersion = 'v1.3.40'
    flipperVersion = '0.52.0'
}

tasks.getByPath('WooCommerce:preBuild').dependsOn installGitHooks

// Onboarding and dev env setup tasks
task checkBundler(type:Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task checkBundle(type:Exec, dependsOn:checkBundler) {
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task applyCredentials(type:Exec, dependsOn:checkBundle) {
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("installDeps") {
    group = 'Onboarding'
    description = 'Install dependencies for production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}
