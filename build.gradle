buildscript {
    ext.kotlinVersion = '1.3.41'
    ext.navigationVersion = '2.0.0'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.automattic.android:fetchstyle:1.1'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

apply plugin: 'com.automattic.android.fetchstyle'

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'kotlin-android-extensions'

    repositories {
        google()
        jcenter()
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion",
                    "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            ]
        }
    }

    configurations {
        ktlint
        compile.exclude group: 'org.jetbrains' , module:'annotations'
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.29.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }

    task ciktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Copies git-hooks from the `tools/team-props/git-hooks' directory to the `.git/hooks` folder
 * at the root of this project.
 */
task installGitHooks(type: Copy) {
    println "Copying git-hooks scripts from tools/team-props/git-hooks to .git/hooks"
    from new File(rootProject.rootDir, 'tools/team-props/git-hooks')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

ext {
    fluxCVersion = '02cc5d4093108b7a671bc6547237a1732c23bf1b'
    daggerVersion = '2.22.1'
    glideVersion = '4.9.0'
    testRunnerVersion = '1.0.1'
    espressoVersion = '3.0.1'
    mockitoKotlinVersion = '2.1.0'
    mockitoVersion = '2.28.2'
    constraintLayoutVersion = '1.1.0'
    multidexVersion = '1.0.3'
    libaddressinputVersion = '0.0.2'
    eventBusVersion = '3.1.1'
    googlePlayCoreVersion = '1.6.1'
    coroutinesVersion = '1.2.2'
    archComponentsVersion = '2.0.0'
    assertjVersion = '3.11.1'
}

tasks.getByPath('WooCommerce:preBuild').dependsOn installGitHooks
