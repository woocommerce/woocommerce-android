import io.github.wzieba.tracks.plugin.TracksExtension

plugins {
    id 'com.automattic.android.fetchstyle'
    id 'io.gitlab.arturbosch.detekt'
    id 'io.github.wzieba.tracks.plugin'
    id 'io.sentry.android.gradle'
    id 'com.android.application' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id 'dagger.hilt.android.plugin' apply false
    id 'com.google.gms.google-services' apply false
}

tracks {
    automatticProject.set(TracksExtension.AutomatticProject.WooCommerce)
    enabled.set((findProperty('tracksEnabled') ?: false).toBoolean())
    username.set(findProperty('tracksUsername'))
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        google()
        mavenCentral()
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {
    configurations {
        compile.exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
}

tasks.register("detektAll", io.gitlab.arturbosch.detekt.Detekt) {
    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    buildUponDefaultConfig = true
    setSource(file(projectDir))
    baseline.set(file("$rootDir/config/detekt/baseline.xml"))
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    include("**/*.kt")
    exclude("**/resources/**", "**/build/**")

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = false
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Copies git-hooks from the `tools/team-props/git-hooks' directory to the `.git/hooks` folder
 * at the root of this project.
 */
tasks.register("installGitHooks", Copy) {
    println "Copying git-hooks scripts from tools/team-props/git-hooks to .git/hooks"
    from new File(rootProject.rootDir, 'tools/team-props/git-hooks')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

ext {
    fluxCVersion = '1.45.1'
    glideVersion = '4.13.2'
    coilVersion = '2.1.0'
    constraintLayoutVersion = '1.2.0'
    libaddressinputVersion = '0.0.2'
    eventBusVersion = '3.3.1'
    googlePlayCoreVersion = '1.10.3'
    coroutinesVersion = '1.6.1'
    lifecycleVersion = '2.4.1'
    aztecVersion = 'v1.3.45'
    flipperVersion = '0.143.0'
    stateMachineVersion = '0.2.0'
    coreKtxVersion = '1.7.0'
    appCompatVersion = '1.4.1'
    materialVersion = '1.6.1'
    hiltJetpackVersion = '1.0.0'
    wordPressUtilsVersion = "2.3.0"
    mediapickerVersion = 'trunk-d0db2634cf27511bec3ef9b837f9cfe27c5ab601'
    wordPressLoginVersion = '0.13.0'
    aboutAutomatticVersion = '0.0.4'

    // Compose and its module versions need to be consistent with each other (for example 'compose-theme-adapter')
    composeVersion = "1.1.1"
    composeAccompanistVersion = "0.23.1"
    lifecycleViewmodelComposeVersion = "2.4.1"
    composeThemeAdapterVersion = "1.1.8"

    // Testing
    jUnitVersion = '4.13.2'
    jUnitExtVersion = '1.1.3'
    androidxTestCoreVersion = '1.4.0'
    assertjVersion = '3.11.1'
    espressoVersion = '3.4.0'
    mockitoKotlinVersion = '4.0.0'
    mockitoVersion = '4.5.1'
}

// Onboarding and dev env setup tasks
task checkBundler(type: Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task checkBundle(type: Exec, dependsOn: checkBundler) {
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task applyCredentials(type: Exec, dependsOn: checkBundle) {
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("configureApply") {
    group = 'Onboarding'
    description = 'Install dependencies for debug and production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}

apply from: './config/gradle/jacoco.gradle'
