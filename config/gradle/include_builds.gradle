def localBuilds = new File("${rootDir}/local-builds.gradle")
if (localBuilds.exists()) {
    apply from: localBuilds

    /*
    In order to add a new included build:
    1. Define a property for its path in the `local-builds.gradle-example` file.
    2. Check if that property exists in `ext` since it may be commented out.
    3. Include the build using the property.
    4. Add dependencySubstitution block and substitute the binary module with the project.
    Note that `project` dependencies are resolved in the context of the included build.
    https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/ConfigurableIncludedBuild.html#dependencySubstitution-org.gradle.api.Action-
    */

    if (ext.has("localFluxCPath")) {
        includeBuild(ext.localFluxCPath) {
            dependencySubstitution {
                println "Substituting fluxc with the local build"
                substitute module(gradle.ext.fluxCBinaryPath) using project(':fluxc')
                substitute module(gradle.ext.fluxCWooCommercePluginBinaryPath) using project(':plugins:woocommerce')
            }
        }
    }

    if (ext.has("localLoginFlowPath")) {
        includeBuild(ext.localLoginFlowPath) {
            dependencySubstitution {
                println "Substituting login-flow with the local build"
                substitute module("$gradle.ext.loginFlowBinaryPath") using project(':WordPressLoginFlow')
            }
        }
    }

    if (ext.has("localMediaPickerPath")) {
        includeBuild(ext.localMediaPickerPath) {
            dependencySubstitution {
                println "Substituting media-picker with the local build"
                substitute module("$gradle.ext.mediaPickerBinaryPath") using project(':mediapicker')
                substitute module("$gradle.ext.mediaPickerDomainBinaryPath") using project(':mediapicker:domain')
                substitute module("$gradle.ext.mediaPickerSourceDeviceBinaryPath") using project(':mediapicker:source-device')
                substitute module("$gradle.ext.mediaPickerSourceGifBinaryPath") using project(':mediapicker:source-gif')
                substitute module("$gradle.ext.mediaPickerSourceWordPressBinaryPath") using project(':mediapicker:source-wordpress')
            }
        }
    }

    if (ext.has("localWooCommerceSharedPath")) {
        includeBuild(ext.localWooCommerceSharedPath) {
            dependencySubstitution {
                println "Substituting WooCommerce-Shared with the local build"
                substitute module("$gradle.ext.wooCommerceSharedBinaryPath") with project(':library')
            }
        }
//        includeBuild(ext.localWooCommerceSharedPath) {
//            dependencySubstitution {
//                println "Substituting WooCommerce-Shared with the local build"
//                substitute module("$gradle.ext.wooCommerceSharedBinaryPath") using project(':library')
//                def reactNativeVersionForSubstitution = '0.71.8'
//                substitute module("com.facebook.react:react-native:+") using module("com.facebook.react:react-android:$reactNativeVersionForSubstitution")
//                substitute module("com.facebook.react:react-android:") using module("com.facebook.react:react-android:$reactNativeVersionForSubstitution")
//                substitute module("com.facebook.react:hermes-android:") using module("com.facebook.react:hermes-android:$reactNativeVersionForSubstitution")
//            }
//        }
    }
}
